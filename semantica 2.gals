#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = false
ScannerTable = Full
Input = Stream
Parser = LL
#RegularDefinitions
L  : [A-Za-z]
D  : [0-9]
WS : [\ \t\n\r] //simbolos especiais e branco
COMMENT : "(*" [^ "*" ]* "*)"  //comentário
#Tokens
//pontuação
"+"
"-"
"*"
"/"
"="
">"
">="
"<"
"<="
"<>"
":="
":"
";"
","
"."
"("
")"

//tokens
ID : {L} ( {L} | {D} | _ )*
INTEIRO : {D}+ //um ou mais dígitos
LITERAL: \" (.)* \"

//palavras chave
PROGRAM = ID : "PROGRAM"
CONST = ID : "CONST"
VAR = ID : "VAR"
PROCEDURE = ID : "PROCEDURE"
BEGIN = ID : "BEGIN"
END = ID : "END"
INTEGER = ID : "INTEGER"
OF = ID : "OF"
CALL = ID : "CALL"
IF = ID : "IF"
THEN = ID : "THEN"
ELSE = ID : "ELSE"
WHILE = ID : "WHILE"
DO = ID : "DO"
REPEAT = ID : "REPEAT"
UNTIL = ID : "UNTIL"
READLN = ID : "READLN"
WRITELN = ID : "WRITELN"
OR = ID : "OR"
AND = ID : "AND"
NOT = ID : "NOT"
FOR = ID : "FOR"
TO = ID : "TO"
CASE = ID : "CASE"

//ignorar espaços em branco, caracteres especiais e comentários
 : {WS}*
 :! {COMMENT}

#NonTerminals
<PROGRAMA>
<BLOCO>
<LID>
<REPIDENT>
<DCLCONST>
<LDCONST>
<DCLVAR>
<LDVAR>
<TIPO>
<DCLPROC>
<DEFPAR>
<CORPO>
<REPCOMANDO>
<COMANDO>
<PARAMETROS>
<REPPAR>
<ELSEPARTE>
<VARIAVEL>
<REPVARIAVEL>
<ITEMSAIDA>
<REPITEM>
<EXPRESSAO>
<REPEXPSIMP>
<EXPSIMP>
<REPEXP>
<TERMO>
<REPTERMO>
<FATOR>
<CONDCASE>
<CONTCASE>
<RPINTEIRO>
#Grammar
 <PROGRAMA> ::= PROGRAM ID #100 ";" <BLOCO> "." #101 ;
 <BLOCO> ::= <DCLCONST> <DCLVAR> #102 <DCLPROC><CORPO> ;
 <LID> ::= ID #104 <REPIDENT> ;
 <REPIDENT> ::= î ;
 <REPIDENT> ::= "," ID #104 <REPIDENT> ;
 <DCLCONST> ::= CONST ID #105 "=" INTEIRO #106 ";" <LDCONST> ;
 <LDCONST> ::= î ;
 <LDCONST> ::= ID #105 "=" INTEIRO #106 ";" <LDCONST> ;
 <DCLCONST> ::= î ;
 <DCLVAR> ::= VAR #107 <LID> ":" <TIPO> ";" <LDVAR> ;
 <LDVAR> ::= î ;
 <LDVAR> ::= <LID> ":" <TIPO> ";" <LDVAR> ;
 <DCLVAR> ::= î ;
 <TIPO> ::= INTEGER ;
 <DCLPROC> ::= PROCEDURE ID #108 <DEFPAR> ";" #109 <BLOCO> ";" #110 <DCLPROC> ;
 <DCLPROC> ::= î ;
 <DEFPAR> ::= î ;
 <DEFPAR> ::= "(" #111 <LID> ":" INTEGER ")" ;
 <CORPO> ::= BEGIN <COMANDO> <REPCOMANDO> END ;
 <REPCOMANDO> ::= î ;
 <REPCOMANDO> ::= ";" <COMANDO> <REPCOMANDO> ;
 <COMANDO> ::= ID #114 ":=" <EXPRESSAO> #115 ;
 <COMANDO> ::= <CORPO> ;
 <COMANDO> ::= î ;
 <COMANDO> ::= CALL ID #116 <PARAMETROS> #117 ;
 <PARAMETROS> ::= î ;
 <PARAMETROS> ::= "(" <EXPRESSAO> #118 <REPPAR> ")" ;
 <REPPAR> ::= î ;
 <REPPAR> ::= "," <EXPRESSAO>#118 <REPPAR> ;
 <COMANDO> ::= IF <EXPRESSAO> #120 THEN <COMANDO> <ELSEPARTE> #121 ;
 <ELSEPARTE> ::= î ;
 <ELSEPARTE> ::= #122 ELSE <COMANDO> ;
 <COMANDO> ::= WHILE #123 <EXPRESSAO> #124 DO <COMANDO> #125 ;
 <COMANDO> ::= REPEAT #126 <COMANDO> UNTIL <EXPRESSAO> #127 ;
 <COMANDO> ::= READLN #128 "(" <VARIAVEL> <REPVARIAVEL> ")" ;
 <VARIAVEL> ::= ID #129 ;
 <REPVARIAVEL>::= î ;
 <REPVARIAVEL>::= "," <VARIAVEL> <REPVARIAVEL> ;
 <COMANDO> ::= WRITELN "(" <ITEMSAIDA> <REPITEM> ")" ;
 <ITEMSAIDA> ::= LITERAL #130 ;
 <ITEMSAIDA> ::= <EXPRESSAO> #131 ;
 <REPITEM> ::= î ;
 <REPITEM> ::= "," <ITEMSAIDA> <REPITEM> ;
 <COMANDO> ::= CASE #132 <EXPRESSAO> OF <CONDCASE> END #133 ;
 <CONDCASE> ::= INTEIRO <RPINTEIRO> ":" #134 <COMANDO> #135 <CONTCASE> ;
 <RPINTEIRO> ::= "," #136 INTEIRO <RPINTEIRO> ;
 <RPINTEIRO> ::= î ;
 <CONTCASE> ::= î ;
 <CONTCASE> ::= ";" <CONDCASE> ;
 <COMANDO> ::= FOR ID #137 ":=" <EXPRESSAO> #138 TO <EXPRESSAO> #139 DO <COMANDO> #140 ;
 <EXPRESSAO> ::= <EXPSIMP> <REPEXPSIMP> ;
 <REPEXPSIMP> ::= î ;
 <REPEXPSIMP> ::= "=" <EXPSIMP> #141 ;
 <REPEXPSIMP> ::= "<" <EXPSIMP> #142 ;
 <REPEXPSIMP> ::= ">" <EXPSIMP> #143 ;
 <REPEXPSIMP> ::= ">=" <EXPSIMP> #144 ;
 <REPEXPSIMP> ::= "<=" <EXPSIMP> #145 ;
 <REPEXPSIMP> ::= "<>" <EXPSIMP> #146 ;
 <EXPSIMP> ::= "+" <TERMO> <REPEXP> ;
 <EXPSIMP> ::= "-" <TERMO> #147 <REPEXP> ;
 <EXPSIMP> ::= <TERMO> <REPEXP> ;
 <REPEXP> ::= "+" <TERMO> #148 <REPEXP> ;
 <REPEXP> ::= "-" <TERMO> #149 <REPEXP> ;
 <REPEXP> ::= OR <TERMO> #150 <REPEXP> ;
 <REPEXP> ::= î ;
 <TERMO> ::= <FATOR> <REPTERMO> ;
 <REPTERMO> ::= î ;
 <REPTERMO> ::= "*" <FATOR> #151 <REPTERMO> ;
 <REPTERMO> ::= "/" <FATOR> #152 <REPTERMO> ;
 <REPTERMO> ::= AND <FATOR> #153 <REPTERMO> ;
 <FATOR> ::= INTEIRO #154 ;
 <FATOR> ::= "(" <EXPRESSAO> ")" ;
 <FATOR> ::= NOT <FATOR> #155 ;
 <FATOR> ::= #156 <VARIAVEL> ;
